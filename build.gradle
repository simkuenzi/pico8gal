plugins {
    id 'java'
    id 'net.researchgate.release' version '2.8.1'
    id "maven-publish"
    id "org.hidetake.ssh" version "2.10.1"
}

group 'com.github.simkuenzi'
version version

java {
    sourceCompatibility JavaVersion.VERSION_14
    targetCompatibility JavaVersion.VERSION_14
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'
    implementation group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-java8time', version: '3.0.4.RELEASE'
    implementation group: 'io.javalin', name: 'javalin', version: '3.9.1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:  '2.11.1'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': "${project.name} server",
                'Implementation-Version': this.version,
                'Main-Class': 'com.github.simkuenzi.pico8gal.Server'
    }
    archiveClassifier.set("all")
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/simkuenzi/pico8gal")
            credentials {
                username = project.ext.properties.gitUser
                password = project.ext.properties.gitToken
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

remotes {
    webServer {
        host = project.ext.properties.deployServer
        user = project.ext.properties.deployUser
        identity = file(project.ext.properties.deployKey ?: 'id_rsa')
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        File d = new File("$buildDir/resources/main/com/github/simkuenzi/pico8gal")
        d.mkdirs()
        new File(d, "version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

task deploy(dependsOn: jar) {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: jar.archiveFile.get().asFile, into: "${project.ext.properties.deployPath}/${project.name}/${project.name}.jar"
            }
        }
    }
}

classes {
    dependsOn createProperties
}

release {
    buildTasks = ['publish', 'deploy']
}